open Utils

(* check that some split corresponds to a single variable split *)
let check_single_var_split children_splits =
    match children_splits with
    | [[(var1, value1, bound_type1)]; [(var2, value2, bound_type2)]] -> var1 = var2 && value1 = value2 && bound_type1 <> bound_type2
    | _ -> false

(** Match a given split with a constraint from a list of known ReLU constraints*)
let rec match_relu_constraint children_splits constraints =
    match constraints with
    | [] -> []
    | (RELU, l)::tl-> (
        match children_splits with
        | [[(var1, _, _); (var2, _, _)]; [(var3, _, _); (var4, _, _)]] ->
            if
                List.exists (fun x -> x = var1) l &&
                List.exists (fun x -> x = var2) l &&
                List.exists (fun x -> x = var3) l &&
                List.exists (fun x -> x = var4) l
            then
                l
            else
                match_relu_constraint children_splits tl
        | _ -> match_relu_constraint children_splits tl
    )
    | (_::tl) -> match_relu_constraint children_splits tl

(* find a constraint given two participating variables *)
let rec match_constraint var1 var2 constraints =
    match constraints with
    | [] -> (UNDEFINED, [])
    | (c, l)::tl->
            if List.exists (fun x -> x = var1) l && List.exists (fun x -> x = var2) l
            then (c, l)
            else match_constraint var1 var2 tl

(* check that the split corresponds to the active phase of a ReLU constraint *)
let is_active split b aux = match split with
    | [(var1, 0., UPPER); (var2, 0., LOWER)] -> var1 = aux && var2 = b
    | [(var1, 0., LOWER); (var2, 0., UPPER)] -> var1 = b && var2 = aux
    | _ -> false

(* check that the split corresponds to the inactive phase of a ReLU constraint *)
let is_inactive split f b = match split with
    | [(var1, 0., UPPER); (var2, 0., UPPER)] -> (var1 = f && var2 = b) || (var1 = b && var2 = f)
    | _ -> false

(* Check whether a proof tree node's split correspond to a known ReLU constraint *)
let check_relu_split (children_splits: (int * float * bound_t) list) constraints =
    let matched_constraint = match_relu_constraint children_splits constraints in
    match matched_constraint, children_splits with
        | [b; f; aux; _], [split1; split2] -> (is_active split1 b aux && is_inactive split2 f b) || (is_active split2 b aux && is_inactive split1 f b)
        | _ -> false

let check_non_empty_split c = match c with
    | ProofTree.Node ([], _, _) | ProofTree.Leaf ([], _, _) -> false
    | _ -> true

let get_child_split c = match c with
    | ProofTree.Node (s, _, _) 
    | ProofTree.Leaf (s, _, _) -> s

