type bound_type = UPPER | LOWER

let bound_type_to_bool = function
    | UPPER -> true
    | LOWER -> false

let bound_type_to_int = function
    | UPPER -> 1i
    | LOWER -> 0i 
    [@@program]

let bound_type_to_str = function
    | UPPER -> "UPPER"
    | LOWER -> "LOWER"
    [@@program]


type constraint_type =
    | RELU
    | MAX
    | SIGN
    | DISJUNCTION
    | ABSOLUTE_VALUE
    | UNDEFINED

let all (l: bool list) = List.fold_left (fun x y -> x && y) true l

let rec repeat x n =
    if n <= 0
    then
        []
    else
        x::(repeat x (n - 1))

lemma repeat_preserve_pos_length x n =
    n >= 0 ==> List.length (repeat x n) = n
[@@auto]

(* set list element at given index to x *)
let rec set_nth (l: 'a list) (n: int) (x: 'a): 'a list =    
    if n < 0 then l
    else
    match l with
    | [] -> []
    (* | [_] when n=0 -> [x] *)
    | hd::tl ->
    if n = 0 then (
        x :: tl
    ) else (
        hd :: set_nth tl (n-1) x
    )
;;

lemma get_set_nth l n a =
    n >= 0 && n < List.length l
     ==>
     (List.nth n (set_nth l n a) = Some a)
[@@auto] [@@rw]
;;

lemma set_nth_unchanged l n m a =
  n <> m
  ==>
  List.nth m (set_nth l n a) = List.nth m l
[@@auto]

(* increment list element at given index by 1 *)
let increment_elem (l: real list) (n: int): real list =
    match List.nth n l with
    | None -> l
    | Some value -> set_nth l n (value +. 1.)

(* decrement list element at given index by 1 *)
let decrement_elem (l: real list) (n: int): real list =
    match List.nth n l with
    | None -> l
    | Some value -> set_nth l n (value -. 1.)

let rec zip (x: 'a list) (y: 'b list): ('a * 'b) list =
    match x, y with
    | [], _ | _, [] -> []
    | hx::tx, hy::ty -> (hx, hy) :: (zip tx ty)

let zip_map_compare (x: 'a list) (y: 'b list) (f: 'a -> 'b -> 'c) (n: int) =
    let open Option in
    let* xn = List.nth n x in
    let* yn = List.nth n y in
    let* b = List.nth n @@ List.map (fun (a, b) -> f a b) (zip x y) in
    Some (f xn yn = b)

lemma zip_map x y f n =
     match zip_map_compare x y f n with
        | Some res -> res = true
        | None -> true
[@@auto]

    (* Add 2 lists of reals element-wise *)
let rec list_add (x: real list) (y: real list): real list = 
    match x, y with
    | [], [] -> []
    | x :: xs, y :: ys -> x +. y :: list_add xs ys
    | x::xs, [] | [], x :: xs -> []

lemma list_add_length_invariant l1 l2 = 
    List.length l1 = List.length l2 
    ==>
    List.length (list_add l1 l2) = List.length l1
    [@@auto][@@induct functional list_add]

(* Multiply all elements in a list of reals by a constant *)
let rec list_scale (x: real list) (c: real): real list = 
    match x with
    | [] -> []
    | x :: xs -> x *. c :: list_scale xs c

lemma list_scale_length_preservation l c = 
    List.length (list_scale l c) = List.length l
    [@@auto]
    [@@rw][@@fc]

lemma repeat_length x n =
    n >= 0 
    ==>
    List.length (repeat x n) = n
[@@auto]

lemma zip_size1 (x: 'a list) (y: 'b list) =
    List.length x <= List.length y 
    ==>
    List.length (zip x y) = List.length x
[@@auto]

lemma zip_size2 (x: 'a list) (y: 'b list) =
    List.length y <= List.length x 
    ==>
    List.length (zip x y) = List.length y
[@@auto]

lemma zip_cons x1 x2 y1 y2 =
    zip (x1 :: x2) (y1 :: y2) = (x1, y1) :: (zip x2 y2)
[@@auto]

lemma zip_cat x1 x2 y1 y2 =
    List.length x1 = List.length y1
    ==> 
    zip (x1 @ x2) (y1 @ y2) = (zip x1 y1) @ (zip x2 y2)
[@@auto]

(** returns true if all elements of l1 are gte to corresponding elements of l2  *)
let list_geq (l1: real list) (l2: real list): bool =
    all (List.map (fun (a, b) -> a >=. b) (zip l1 l2))

(* Utils to_string functions for debugging *)
(* let rec poly_to_string p = 
  match p with 
  | [] -> "\n"
  | p :: ps -> Printf.sprintf "%f; " (Q.to_float p) ^ poly_to_string ps
[@@program]

let rec system_to_string system = 
  match system with 
  | [] -> "\n"
  | Geq e :: es -> "Geq " ^ poly_to_string e ^ system_to_string es
  | Eq e :: es -> "Eq " ^ poly_to_string e ^ system_to_string es
  | Gt e :: es -> "Gt " ^ poly_to_string e ^ system_to_string es
[@@program] *)